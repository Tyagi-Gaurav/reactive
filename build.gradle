buildscript {
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
}

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "findbugs"

    findbugs {
        toolVersion = "3.0.1"
        ignoreFailures = false
        effort = "max"
        excludeFilter = file("$rootDir/gradle/config/findbugs/exclude.xml")
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group 'gt.reactive'

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    apply from: "${it.rootDir}/gradle/dependencies.gradle"

    project.version = System.getenv("BUILD_NAME") ? scmVersion.version : "DEV"

    project.ext {
        springBootVersion = '2.1.0.RELEASE'
    }

    jar {
        manifest {
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    task buildImageDependencies(dependsOn : ["compile", "jar"])

    project.plugins.withId("net.ltgt.apt") {
        try {
            // automatically apply net.ltgt.apt-idea whenever net.ltgt.apt is used
            project.apply plugin: "net.ltgt.apt-idea"
            // disable addAptDependencies (if you delegate build actions to Gradle)
            project.plugins.withType(JavaPlugin) {
                project.afterEvaluate {
                    project.idea.module.apt.addAptDependencies = false
                }
            }
        } catch (UnknownPluginException) {
            // ignore, in case an older version of net.ltgt.apt is being used
            // that doesn't come with net.ltgt.apt-idea.
        }
    }
}

scmVersion {
    tag {
        prefix = project.name
    }

    versionIncrementer 'incrementMinor'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

defaultTasks 'clean', 'test', 'build'